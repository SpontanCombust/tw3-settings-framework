abstract class ISettingsMaster
{
    public const var modVersion: string;
    
    public var id: name;


    // ============================ Main functions, can be overriden  ==============================

    // Initializes the settings class members and more
    public function Init() : void 
    {
        LogChannel('ModSettingsFramework', "Initialising settings master '" + id + "'");
        Parser_Init();

        if(ShouldResetSettingsToDefaultOnInit())
        {
            ResetSettingsToDefault();
        }
    }

    // Corrects values to ranges specified in the xml
    public function ValidateSettings() : void
    {
        LogChannel('ModSettingsFramework', "Validating settings for '" + id + "'");
        Parser_ValidateSettings();
    }

    // Reads all settings from CInGameConfigWrapper using ReadSettingValue and sets class variables
    public function ReadSettings() : void 
    {
        var config: CInGameConfigWrapper;
        config = theGame.GetInGameConfigWrapper();

        LogChannel('ModSettingsFramework', "Reading settings for '" + id + "'");
        Parser_ReadSettings(config);
    }

    // Using class variables and WriteSettingValue sets all settings in CInGameConfigWrapper and saves user configuration
    public function WriteSettings() : void 
    {
        var config: CInGameConfigWrapper;
        config = theGame.GetInGameConfigWrapper();

        LogChannel('ModSettingsFramework', "Writing settings for '" + id + "'");
        Parser_WriteSettings(config);
        theGame.SaveUserSettings();
    }

    // Apply a default preset to all groups if possible
    public function ResetSettingsToDefault() : void
    {
        var config: CInGameConfigWrapper;
        config = theGame.GetInGameConfigWrapper();

        LogChannel('ModSettingsFramework', "Resetting settings to default for '" + id + "'");
        Parser_ResetSettingsToDefault(config);
        theGame.SaveUserSettings();
    }

    // Checks whether this mod's settings have been saved onto disk before
    public function ShouldResetSettingsToDefaultOnInit() : bool
    {
        return Parser_ShouldResetSettingsToDefaultOnInit();
    }

    // Returns integer value of the unified enum type for options var index in user config
    // If the config value is not valid for given option, should return -1
    public function EnumValueMappingConfigToUnified(gId: name, vId: name, val: int) : int
    {
        return Parser_EnumValueMappingConfigToUnified(gId, vId, val);
    }

    // Returns the options var index in user config for integer value of unified enum
    // If the unified value is not valid for given option, should return -1
    public function EnumValueMappingUnifiedToConfig(gId: name, vId: name, val: int) : int
    {
        return Parser_EnumValueMappingUnifiedToConfig(gId, vId, val);
    }

    // If integer value for given enum variable is correct returns said value
    // Otherwise returns the smallest valid value
    public function EnumValueMappingValidateUnified(gId: name, vId: name, val: int) : int
    {
        return Parser_EnumValueMappingValidateUnified(gId, vId, val);
    }




    // ==== Get/Set functions - to be potentially overriden by the developer if default is not enough ====

    // Fetches setting value from CInGameConfigWrapper
    public function ReadSettingValue(config: CInGameConfigWrapper, gId: name, vId: name) : string
    {
        return config.GetVarValue(gId, vId);
    }

    // Writes setting value into CInGameConfigWrapper
    public function WriteSettingValue(config: CInGameConfigWrapper, gId: name, vId: name, value: string) : void
    {
        config.SetVarValue(gId, vId, value);
    }

    // Applies a preset to a group in CInGameConfigWrapper
    public function ResetSettingValues(config: CInGameConfigWrapper, gId: name, presetIndex: int) : void
    {
        config.ApplyGroupPreset(gId, presetIndex);
    }



    // ====================== Functions to be defined in class generated by parser  ======================

    protected function Parser_Init() : void {}
    protected function Parser_ValidateSettings() : void {}
    protected function Parser_ReadSettings(config: CInGameConfigWrapper) : void {}
    protected function Parser_WriteSettings(config: CInGameConfigWrapper) : void {}
    protected function Parser_ResetSettingsToDefault(config: CInGameConfigWrapper) : void {}
    protected function Parser_ShouldResetSettingsToDefaultOnInit(config: CInGameConfigWrapper) : bool { return false; }
    protected function Parser_EnumValueMappingConfigToUnified(gId: name, vId: name, val: int) : int { return -1; }
    protected function Parser_EnumValueMappingUnifiedToConfig(gId: name, vId: name, val: int) : int { return -1; }
    protected function Parser_EnumValueMappingValidateUnified(gId: name, vId: name, val: int) : int { return 0; }


    // ====================== Utility functions ======================

    // these bool conversion functions are here for sanity sake, 
    // because an implicit conversion from string to bool doesn't sit right with me
    public function StringToBool(s: string) : bool
    {
        if(s == "false" || s == "" || !s) {
            return false;
        } else {
            return true;
        }
    }

    public function BoolToString(b: bool) : string
    {
        if(b) {
            return "true";
        } else {
            return "false";
        }
    }

    //TODO move to ISettingsGroup
    public function ReadIntSettingValue(config: CInGameConfigWrapper, gId: name, vId: name) : int
    {
        return StringToInt(ReadSettingValue(config, gId, vId), 0);
    }

    public function ReadFloatSettingValue(config: CInGameConfigWrapper, gId: name, vId: name) : float
    {
        return StringToFloat(ReadSettingValue(config, gId, vId), 0.0);
    }

    public function ReadBoolSettingValue(config: CInGameConfigWrapper, gId: name, vId: name) : bool
    {
        return StringToBool(ReadSettingValue(config, gId, vId));
    }

    public function ReadUnifiedEnumSettingValue(config: CInGameConfigWrapper, gId: name, vId: name) : int
    {
        return EnumValueMappingConfigToUnified(gId, vId, ReadIntSettingValue(config, gId, vId));
    }


    public function WriteIntSettingValue(config: CInGameConfigWrapper, gId: name, vId: name, value: int) : void
    {
        WriteSettingValue(config, gId, vId, IntToString(value));
    }

    public function WriteFloatSettingValue(config: CInGameConfigWrapper, gId: name, vId: name, value: float) : void
    {
        WriteSettingValue(config, gId, vId, FloatToString(value));
    }

    public function WriteBoolSettingValue(config: CInGameConfigWrapper, gId: name, vId: name, value: bool) : void
    {
        WriteSettingValue(config, gId, vId, BoolToString(value));
    }

    public function WriteUnifiedEnumSettingValue(config: CInGameConfigWrapper, gId: name, vId: name, value: int) : void
    {
        WriteIntSettingValue(config, gId, vId, EnumValueMappingUnifiedToConfig(gId, vId, value));
    }
}